# CMakeLists.txtが存在するサブディレクトリをすべて走査する
MACRO(ADD_SUBDIRS)
  # 引数を指定すると、そのディレクトリを add_subdirectory から除外する
  FILE(GLOB sub_dirs RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} *)
  SET(exclude_list "${ARGN}")
  list(LENGTH exclude_list exclude_num)
  FOREACH(dir IN LISTS sub_dirs)
    IF (${exclude_num} GREATER 0)
      IF (dir IN_LIST exclude_list)
        continue()
      ENDIF()
    ENDIF()
    IF (IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${dir})
      IF (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${dir}/CMakeLists.txt)
        add_subdirectory(${dir})
      ENDIF()
    ENDIF()
  ENDFOREACH()
ENDMACRO()

# executableを追加、作成する
MACRO(ADD_SNIPETS)
  if ("${module_name}" STREQUAL "")
    get_filename_component(module_name ${CMAKE_CURRENT_SOURCE_DIR} NAME_WE)
  endif()
  FILE(GLOB file_list "${CMAKE_CURRENT_SOURCE_DIR}/[a-z]*.cpp")
  FOREACH(filename IN LISTS file_list)
    get_filename_component(basename ${filename} NAME_WE)
    add_executable(${module_name}_${basename} ${filename})
    target_link_libraries(${module_name}_${basename} ${LIBRARIES} ${EXTERNAL_LIBS})
    if (MSVC)
      set_property(TARGET ${module_name}_${basename} PROPERTY
        MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>DLL")
    endif()
  ENDFOREACH()
ENDMACRO()

# libraryを作成する
MACRO(ADD_LIB)
  if ("${module_name}" STREQUAL "")
    get_filename_component(module_name ${CMAKE_CURRENT_SOURCE_DIR} NAME_WE)
  endif()
  FILE(GLOB file_list "${CMAKE_CURRENT_SOURCE_DIR}/[a-z]*.cpp")
  if (file_list)
    add_library(${module_name} ${file_list})
    set(LIBRARIES ${LIBRARIES} ${module_name} CACHE INTERNAL "")
    # message("${module_name} : ${file_list}")
  endif()
ENDMACRO()

# 指定したdirectoryのlibraryを作成する
MACRO(CREATE_LIB_FROM_DIR module_name directory)
  message("directory : ${directory}")
  FILE(GLOB_RECURSE file_list LIST_DIRECTORIES true "${directory}/[a-z]*.cpp")
  if (file_list)
    if (BUILD_SHARED_LIBS)
      add_library(${module_name} SHARED ${file_list})
      target_link_libraries(${module_name} ${EXTERNAL_LIBS})
    else()
      add_library(${module_name} STATIC ${file_list})
    endif()
    set(LIBRARIES ${LIBRARIES} ${module_name} CACHE INTERNAL "")
  endif()
ENDMACRO()

MACRO(ADD_GTEST)
  if (NOT "${module_name}")
    get_filename_component(module_name ${CMAKE_CURRENT_SOURCE_DIR} NAME_WE)
  endif()

  file(GLOB file_list "${CMAKE_CURRENT_SOURCE_DIR}/test_*.cpp")
  foreach (fname IN LISTS file_list)
    get_filename_component(bname ${fname} NAME_WE)
    set(test_name "gtest_${module_name}_${bname}")
    add_executable(${test_name} ${fname})
    target_link_libraries(${test_name} ${LIBRARIES} ${EXTERNAL_LIBS} ${HALIDE_OBJECTS})
    add_test(NAME ${test_name}
      COMMAND "$<TARGET_FILE:${test_name}>")
  endforeach()
ENDMACRO()
